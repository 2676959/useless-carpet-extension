plugins {
    id 'maven-publish'
    id 'fabric-loom' version '1.10-SNAPSHOT' apply false
    // https://github.com/ReplayMod/preprocessor
    // https://github.com/Fallen-Breath/preprocessor
    id 'com.replaymod.preprocess' version '0a04a5668b'
}

preprocess {
    strictExtraMappings = false

    def mc1202 = createNode('1.20.2', 1_20_02, '')
    def mc1204 = createNode('1.20.4', 1_20_04, '')

    mc1202.link(mc1204, file('versions/mapping-1.20.2-1.20.4.txt'))
}

tasks.register('buildAndGather') {
    subprojects {
        dependsOn project.tasks.named('build').get()
    }
    doFirst {
        println 'Gathering builds'
        def buildLibs = {
            p -> p.buildDir.toPath().resolve('libs')
        }
        delete fileTree(buildLibs(rootProject)) {
            include '*'
        }
        subprojects {
            copy {
                from(buildLibs(project)) {
                    include '*.jar'
                    exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
                }
                into buildLibs(rootProject)
                duplicatesStrategy = DuplicatesStrategy.INCLUDE
            }
        }
    }
}
